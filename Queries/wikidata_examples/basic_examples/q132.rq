# German states, ordered by the number of company headquarters per million inhabitants
# Replace wd:Q1221156 (state of Germany) by anything else you care about, e.g., wd:Q6256 (country)
# Nested queries for correctness (don't count companies twice) and performance (aggregate before adding more data)
# Limits: states without population missing; company data in Wikidata is always incomplete
# Note: This query shows some not-so-easy nesting of DISTINCT (don't count any company twice) and aggregate, in combination with arithmetic output evaluation functions. It is a hard query that may time out if modified.
SELECT
  ?state
  ?companies
  ?population
  (?companies/?population*1000000 AS ?companiesPerM)
WHERE
{
  { SELECT ?state (count(*) as ?companies) WHERE {
    {SELECT DISTINCT ?company ?state WHERE {
      ?state wdt:P31 wd:Q1221156 .
      ?company wdt:P31/wdt:P279* wd:Q4830453 .
      ?company wdt:P159/wdt:P131* ?state .
      FILTER NOT EXISTS{ ?company wdt:P576 ?date } # don't count dissolved companies
    } }
  } GROUP BY ?state  }
    ?state wdt:P1082 ?population
}
ORDER BY DESC(?companiesPerM)